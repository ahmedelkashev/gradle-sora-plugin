plugins {
//    id 'nebula.lint' version '9.3.2'
    id "com.palantir.idea-test-fix" version "0.1.0"
    id 'groovy'
    id 'maven'
    id 'maven-publish'
    id 'idea'
    id "com.gradle.plugin-publish" version "0.10.1"
    id "java-gradle-plugin"
    id 'com.palantir.git-version' version '0.12.0-rc2'
}


group 'jp.co.soramitsu'
version '0'

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

//mavenCoordinates {
//    groupId = "jp.co.soramitsu"
//    artifactId = "sora-plugin"
//    version = version
//}


dependencies {
    implementation gradleApi()
    implementation localGroovy()
    testImplementation gradleTestKit()

    implementation 'com.bmuschko:gradle-docker-plugin:4.5.0'
    implementation 'gradle.plugin.com.palantir.gradle.gitversion:gradle-git-version:0.12.0-rc2'


//    // for mocking in groovy
//    testCompile("net.bytebuddy:byte-buddy:${bytebuddyVersion}")
//    testCompile("org.objenesis:objenesis:${objgenesisVersion}")
//    testCompile("cglib:cglib-nodep:${cglibVersion}")

//    compileOnly("org.projectlombok:lombok:${lombokVersion}")
//    testCompileOnly("org.projectlombok:lombok:${lombokVersion}")

//    testCompile("com.nagternal:spock-genesis:${spockGenesisVersion}")

    testImplementation('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Sora Devops Helper plugin',
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from groovydoc.destinationDir
}

artifacts {
    archives groovydocJar
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
    }
}

gradlePlugin {
    plugins {
        soraPlugin {
            id = 'soramitsu'
            implementationClass = 'jp.co.soramitsu.devops.SoraPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/soramitsu/gradle-sora-plugin'
    vcsUrl = 'https://github.com/soramitsu/gradle-sora-plugin'
    description = 'Gradle plugin that provides tasks to unify tasks for gradle projects across Soramitsu projects.'
    tags = ['soramitsu', 'gradle']

    plugins {
        soraPlugin {
            // id is captured from java-gradle-plugin configuration
            displayName = 'Gradle Sora plugin'
        }
    }
}
